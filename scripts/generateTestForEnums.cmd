@ECHO OFF

CALL "%~dp0\validateCygwinBinaries.cmd"
IF "1"=="%ERRORLEVEL%" (
    @ECHO Invalid or incomplete Cygwin installation. Install cygwin and its components viz.
    @ECHO grep sed perl cut touch wget sort
    EXIT /b 1
)
SET CYGWIN_EXE=%CYGWIN_HOME%\bin

@ECHO Generating enum tests
SET SRCDIR=Authorize.NET\Api\Contracts\V1
IF NOT EXIST "%SRCDIR%" (
    @ECHO "%SRCDIR%" Does not exist
    EXIT /b 1
)
SET CYGWIN=NODOSFILEWARNING
SET OUTFILE=%TEMP%\AllGeneratedEnumTest.cs
SET TEMPLATE=%CD%\scripts\EnumTemplate.cst
PUSHD "%SRCDIR%"
"%CYGWIN_EXE%\grep.exe" -i "public enum" AnetApiSchema.generated.cs | "%CYGWIN_EXE%\cut.exe" -c17- | "%CYGWIN_EXE%\cut.exe" -f1 -d" " > %TEMP%\enum.lst

@ECHO.> "%OUTFILE%"
@ECHO         //Generated by cs-enum-test on %date%-%time% >> "%OUTFILE%"
@ECHO         [Test] >> "%OUTFILE%"
@ECHO         public void AllEnumTest() >> "%OUTFILE%"
@ECHO         {>> "%OUTFILE%"
@ECHO.
@ECHO.

FOR /f %%x IN ( %TEMP%\enum.lst) DO (
    @ECHO Processing %%x;
    COPY %TEMPLATE% %TEMP%\%%x.cs 1>NUL
    "%CYGWIN_EXE%\perl.exe" -pi -w -e 's/ENUMNAME/%%x/g;' %TEMP%\%%x.cs 2>NUL
    TYPE %TEMP%\%%x.cs >> "%OUTFILE%"
)
@ECHO         } >> "%OUTFILE%"
@ECHO.>> "%OUTFILE%"
POPD
@ECHO.
@ECHO.
@ECHO The generated test is in "%OUTFILE%"
